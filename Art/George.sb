' George in the Small Basic World
' Written by Nonki Takahashi.
'
' History:
'  2014-02-24 Corrected arms of George. (XWN777-0)
'  2014-02-23 Added ground and mobility. (XWN777)
'  2014-02-23 20:11:56 Shapes generated by Shapes 1.6b.
'
' initialize shapes
SB_Workaround()
GraphicsWindow.Title = "George in the Small Basic World"
GraphicsWindow.BackgroundColor = "LightCyan"
DrawGround()
GraphicsWindow.BrushColor = "Gray"
GraphicsWindow.FontName = "Tahoma"
GraphicsWindow.FontSize = 40
hello = Shapes.AddText("Hello World!")
Shapes.SetOpacity(hello, 0)
len = 249 ' measured with Measur 0.1 (KTK906-0)
x = (gw - len) / 2
Shapes.Move(hello, x, 60)
Shapes_Init()
' add shapes
scale = 1
angle = 0
iMin = 1
iMax = 33
Shapes_Add()
For x = 540 To -180 Step -60
  If x = 180 Then
    For o = 0 To 100 Step 10
      Program.Delay(400)
      Shapes.SetOpacity(hello, o)
    EndFor
    For o = 100 To 0 Step -10
      Program.Delay(400)
      Shapes.SetOpacity(hello, o)
    EndFor
  Else
    Program.Delay(800)
  EndIf
  If Math.Remainder(x, 120) = 0 Then
    y = 160
  Else
    y = 152
  EndIf
  Shapes_Move()
EndFor
Sub DrawGround
  gw = 598
  gh = 428
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  GraphicsWindow.PenWidth = 1
  GraphicsWindow.PenColor = "#333333"
  GraphicsWindow.BrushColor = "LightGray"
  For x = 4 To gw Step 20
    GraphicsWindow.FillRectangle(x, gh - 28, 12, 5)
    GraphicsWindow.DrawRectangle(x, gh - 28, 12, 5)
  EndFor
  GraphicsWindow.FillRectangle(0, gh - 24, gw, 100)
  GraphicsWindow.DrawLine(0, gh - 24, gw, gh - 24)
EndSub
Sub Shapes_Init
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 600 ' x offset
  shY = 160 ' y offset
  shape = ""
  ' head
  shape[1] = "func=rect;x=24;y=0;width=12;height=5;bc=#FCCE27;pc=#333333;pw=1;"
  shape[2] = "func=rect;x=44;y=0;width=12;height=5;bc=#FCCE27;pc=#333333;pw=1;"
  shape[3] = "func=rect;x=64;y=0;width=12;height=5;bc=#FCCE27;pc=#333333;pw=1;"
  shape[4] = "func=rect;x=84;y=0;width=12;height=5;bc=#FCCE27;pc=#333333;pw=1;"
  shape[5] = "func=rect;x=20;y=4;width=81;height=25;bc=#FCCE27;pc=#333333;pw=1;"
  shape[6] = "func=rect;x=4;y=24;width=12;height=5;bc=#FCCE27;pc=#333333;pw=1;"
  shape[7] = "func=rect;x=0;y=28;width=61;height=25;bc=#FCCE27;pc=#333333;pw=1;"
  shape[8] = "func=rect;x=104;y=24;width=12;height=5;bc=#FCCE27;pc=#333333;pw=1;"
  shape[9] = "func=rect;x=100;y=28;width=21;height=25;bc=#FCCE27;pc=#333333;pw=1;"
  ' face
  shape[10] = "func=rect;x=60;y=28;width=41;height=25;bc=#FFFFFF;pc=#333333;pw=1;"
  shape[11] = "func=rect;x=80;y=52;width=41;height=25;bc=#FFFFFF;pc=#333333;pw=1;"
  shape[12] = "func=rect;x=20;y=52;width=21;height=25;bc=#000000;pc=#333333;pw=1;"
  shape[13] = "func=rect;x=40;y=52;width=21;height=25;bc=#FFFFFF;pc=#333333;pw=1;"
  shape[14] = "func=rect;x=60;y=52;width=21;height=25;bc=#000000;pc=#333333;pw=1;"
  shape[15] = "func=rect;x=20;y=76;width=81;height=25;bc=#FFFFFF;pc=#333333;pw=1;"
  ' body
  shape[16] = "func=rect;x=4;y=96;width=12;height=5;bc=#BE1509;pc=#333333;pw=1;"
  shape[17] = "func=rect;x=0;y=100;width=41;height=25;bc=#BE1509;pc=#333333;pw=1;"
  shape[18] = "func=rect;x=40;y=100;width=21;height=25;bc=#000000;pc=#333333;pw=1;"
  shape[19] = "func=rect;x=104;y=96;width=12;height=5;bc=#BE1509;pc=#333333;pw=1;"
  shape[20] = "func=rect;x=60;y=100;width=61;height=25;bc=#BE1509;pc=#333333;pw=1;"
  shape[21] = "func=rect;x=20;y=124;width=21;height=25;bc=#BE1509;pc=#333333;pw=1;"
  shape[22] = "func=rect;x=40;y=124;width=21;height=25;bc=#000000;pc=#333333;pw=1;"
  shape[23] = "func=rect;x=60;y=124;width=41;height=25;bc=#BE1509;pc=#333333;pw=1;"
  shape[24] = "func=rect;x=20;y=148;width=81;height=25;bc=#BE1509;pc=#333333;pw=1;"
  ' arms
  shape[25] = "func=rect;x=0;y=124;width=21;height=25;bc=#BE1509;pc=#333333;pw=1;"
  shape[26] = "func=rect;x=0;y=148;width=21;height=25;bc=#FFFFFF;pc=#333333;pw=1;"
  shape[27] = "func=rect;x=100;y=124;width=21;height=25;bc=#BE1509;pc=#333333;pw=1;"
  shape[28] = "func=rect;x=100;y=148;width=21;height=25;bc=#FFFFFF;pc=#333333;pw=1;"
  ' bottom
  shape[29] = "func=rect;x=20;y=172;width=81;height=25;bc=#095ABE;pc=#333333;pw=1;"
  shape[30] = "func=rect;x=20;y=196;width=21;height=25;bc=#095ABE;pc=#333333;pw=1;"
  shape[31] = "func=rect;x=80;y=196;width=21;height=25;bc=#095ABE;pc=#333333;pw=1;"
  shape[32] = "func=rect;x=0;y=220;width=41;height=25;bc=#000000;pc=#333333;pw=1;"
  shape[33] = "func=rect;x=60;y=220;width=41;height=25;bc=#000000;pc=#333333;pw=1;"
EndSub
Sub Math_CartesianToPolar
  ' Math | convert cartesian coodinate to polar coordinate
  ' param x, y - cartesian coordinate
  ' return r, a - polar coordinate
  r = Math.SquareRoot(x * x + y * y)
  If x = 0 And y > 0 Then
    a = 90 ' [degree]
  ElseIf x = 0 And y < 0 Then
    a = -90
  Else
    a = Math.ArcTan(y / x) * 180 / Math.Pi
  EndIf
  If x < 0 Then
    a = a + 180
  ElseIf x > 0 And y < 0 Then
    a = a + 360
  EndIf
EndSub
Sub SB_RotateWorkaround
  ' Small Basic | Rotate workaround for Silverlight
  ' param x, y - original coordinate
  ' param alpha - angle [radian]
  ' returns x, y - workaround coordinate
  If shape[i]["func"] = "tri" Then
    x1 = -Math.Floor(shape[i]["x3"] / 2)
    y1 = -Math.Floor(shape[i]["y3"] / 2)
  ElseIf shape[i]["func"] = "line" Then
    x1 = -Math.Floor(Math.Abs(shape[i]["x1"] - shape[i]["x2"]) / 2)
    y1 = -Math.Floor(Math.Abs(shape[i]["y1"] - shape[i]["y2"]) / 2)
  EndIf
  ox = x - x1
  oy = y - y1
  x = x1 * Math.Cos(alpha) - y1 * Math.Sin(alpha) + ox
  y = x1 * Math.Sin(alpha) + y1 * Math.Cos(alpha) + oy
EndSub
Sub SB_Workaround
  ' Small Basic | Workaround for Silverlight
  ' returns silverlight - "True" if in remote
  color = GraphicsWindow.GetPixel(0, 0)
  If Text.GetLength(color) > 7 Then
    silverlight = "True"
    msWait = 300
  Else
    silverlight = "False"
  EndIf
EndSub
Sub Shapes_Add
  ' Shapes | add shapes as shapes data
  ' param iMin, iMax - shape indices to add
  ' param shape - array of shapes
  ' param scale - 1 if same scale
  ' return shWidth, shHeight - total size of shapes
  ' return shAngle - current angle of shapes
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  Shapes_CalcWidthAndHeight()
  s = scale
  For i = iMin To iMax
    GraphicsWindow.PenWidth = shape[i]["pw"] * s
    If shape[i]["pw"] > 0 Then
      GraphicsWindow.PenColor = shape[i]["pc"]
    EndIf
    If Text.IsSubText("rect|ell|tri|text", shape[i]["func"]) Then
      GraphicsWindow.BrushColor = shape[i]["bc"]
    EndIf
    If shape[i]["func"] = "rect" Then
      shape[i]["obj"] = Shapes.AddRectangle(shape[i]["width"] * s, shape[i]["height"] * s)
    ElseIf shape[i]["func"] = "ell" Then
      shape[i]["obj"] = Shapes.AddEllipse(shape[i]["width"] * s, shape[i]["height"] * s)
    ElseIf shape[i]["func"] = "tri" Then
      shape[i]["obj"] = Shapes.AddTriangle(shape[i]["x1"] * s, shape[i]["y1"] * s, shape[i]["x2"] * s, shape[i]["y2"] * s, shape[i]["x3"] * s, shape[i]["y3"] * s)
    ElseIf shape[i]["func"] = "line" Then
      shape[i]["obj"] = Shapes.AddLine(shape[i]["x1"] * s, shape[i]["y1"] * s, shape[i]["x2"] * s, shape[i]["y2"] * s)
    ElseIf shape[i]["func"] = "text" Then
      If silverlight Then
        fs = Math.Floor(shape[i]["fs"] * 0.9)
      Else
        fs = shape[i]["fs"]
      EndIf
      GraphicsWindow.FontSize = fs * s
      GraphicsWindow.FontName = shape[i]["fn"]
      shape[i]["obj"] = Shapes.AddText(shape[i]["text"])
    EndIf
    x = shape[i]["x"]
    y = shape[i]["y"]
    shape[i]["rx"] = x
    shape[i]["ry"] = y
    If silverlight And Text.IsSubText("tri|line", shape[i]["func"]) Then
      alpha = Math.GetRadians(shape[i]["angle"])
      SB_RotateWorkaround()
      shape[i]["wx"] = x
      shape[i]["wy"] = y
    EndIf
    Shapes.Move(shape[i]["obj"], shX + x * s, shY + y * s)
    If Text.IsSubText("rect|ell|tri|text", shape[i]["func"]) And shape[i]["angle"] <> 0 Then
      Shapes.Rotate(shape[i]["obj"], shape[i]["angle"])
    EndIf
  EndFor
  shAngle = 0
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub
Sub Shapes_CalcRotatePos
  ' Shapes | Calculate position for rotated shape
  ' param["x"], param["y"] - position of a shape
  ' param["width"], param["height"] - size of a shape
  ' param ["cx"], param["cy"] - center of rotation
  ' param ["angle"] - rotate angle
  ' return x, y - rotated position of a shape
  _cx = param["x"] + param["width"] / 2
  _cy = param["y"] + param["height"] / 2
  x = _cx - param["cx"]
  y = _cy - param["cy"]
  Math_CartesianToPolar()
  a = a + param["angle"]
  x = r * Math.Cos(a * Math.Pi / 180)
  y = r * Math.Sin(a * Math.Pi / 180)
  _cx = x + param["cx"]
  _cy = y + param["cy"]
  x = _cx - param["width"] / 2
  y = _cy - param["height"] / 2
EndSub
Sub Shapes_CalcWidthAndHeight
  ' Shapes | Calculate total width and height of shapes
  ' param iMin, iMax - shape indices to add
  ' return shWidth, shHeight - total size of shapes
  For i = iMin To iMax
    If shape[i]["func"] = "tri" Or shape[i]["func"] = "line" Then
      xmin = shape[i]["x1"]
      xmax = shape[i]["x1"]
      ymin = shape[i]["y1"]
      ymax = shape[i]["y1"]
      If shape[i]["x2"] < xmin Then
        xmin = shape[i]["x2"]
      EndIf
      If xmax < shape[i]["x2"] Then
        xmax = shape[i]["x2"]
      EndIf
      If shape[i]["y2"] < ymin Then
        ymin = shape[i]["y2"]
      EndIf
      If ymax < shape[i]["y2"] Then
        ymax = shape[i]["y2"]
      EndIf
      If shape[i]["func"] = "tri" Then
        If shape[i]["x3"] < xmin Then
          xmin = shape[i]["x3"]
        EndIf
        If xmax < shape[i]["x3"] Then
          xmax = shape[i]["x3"]
        EndIf
        If shape[i]["y3"] < ymin Then
          ymin = shape[i]["y3"]
        EndIf
        If ymax < shape[i]["y3"] Then
          ymax = shape[i]["y3"]
        EndIf
      EndIf
      shape[i]["width"] = xmax - xmin
      shape[i]["height"] = ymax - ymin
    EndIf
    If i = 1 Then
      shWidth = shape[i]["x"] + shape[i]["width"]
      shHeight = shape[i]["y"] + shape[i]["height"]
    Else
      If shWidth < shape[i]["x"] + shape[i]["width"] Then
        shWidth = shape[i]["x"] + shape[i]["width"]
      EndIf
      If shHeight < shape[i]["y"] + shape[i]["height"] Then
        shHeight = shape[i]["y"] + shape[i]["height"]
      EndIf
    EndIf
  EndFor
EndSub
Sub Shapes_Move
  ' Shapes | Move shapes
  ' param iMin, iMax - shape indices to add
  ' param shape - array of shapes
  ' param scale - to zoom
  ' param x, y - position to move
  ' return shX, shY - new position of shapes
  Stack.PushValue("local", i)
  s = scale
  shX = x
  shY = y
  For i = iMin To iMax
    If silverlight And Text.IsSubText("tri|line", shape[i]["func"]) Then
      _x = shape[i]["wx"]
      _y = shape[i]["wy"]
    Else
      _x = shape[i]["rx"]
      _y = shape[i]["ry"]
    EndIf
    Shapes.Move(shape[i]["obj"], shX + _x * s, shY + _y * s)
  EndFor
  i = Stack.PopValue("local")
EndSub
Sub Shapes_Remove
  ' Shapes | Remove shapes
  ' param iMin, iMax - shapes indices to remove
  ' param shape - array of shapes
  Stack.PushValue("local", i)
  For i = iMin To iMax
    Shapes.Remove(shape[i]["obj"])
  EndFor
  i = Stack.PopValue("local")
EndSub
Sub Shapes_Rotate
  ' Shapes | Rotate shapes
  ' param iMin, iMax - shapes indices to rotate
  ' param shape - array of shapes
  ' param scale - to zoom
  ' param angle - to rotate
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  s = scale
  param["angle"] = angle
  param["cx"] = shWidth / 2
  param["cy"] = shHeight / 2
  For i = iMin To iMax
    param["x"] = shape[i]["x"]
    param["y"] = shape[i]["y"]
    param["width"] = shape[i]["width"]
    param["height"] = shape[i]["height"]
    Shapes_CalcRotatePos()
    shape[i]["rx"] = x
    shape[i]["ry"] = y
    If silverlight And Text.IsSubText("tri|line", shape[i]["func"]) Then
      alpha = Math.GetRadians(angle + shape[i]["angle"])
      SB_RotateWorkAround()
      shape[i]["wx"] = x
      shape[i]["wy"] = y
    EndIf
    Shapes.Move(shape[i]["obj"], shX + x * s, shY + y * s)
    Shapes.Rotate(shape[i]["obj"], angle + shape[i]["angle"])
  EndFor
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub
