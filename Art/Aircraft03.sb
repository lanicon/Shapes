' Aircraft
' Version 0.3
' Copyright Â© 2015-2018 Nonki Takahashi.  The MIT License.
' 2015-07-02 16:05:14 Shapes generated by Shapes 2.1b.
' Last update 2018-12-03
' Program ID NVT854-0

title = "Aircraft 0.3"
SB_Workaround()
gw = 598
gh = 428
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.BackgroundColor = "SkyBlue"
GraphicsWindow.PenWidth = 0
GraphicsWindow.BrushColor = "Gray"
ground = Shapes.AddRectangle(gw, gh - 300)
Shapes.Move(ground, 0, 300)
GraphicsWindow.FontName = "Trubchet MS"
GraphicsWindow.FontSize = 12
GraphicsWindow.BrushColor = "White"
titleObj = Shapes.AddText(title)
Shapes.Move(titleObj, 10, gh - 20)
' initialize shapes
Shapes_Init()
' add shapes
scale = 1 / 2
angle = 0
iMin = 1
iMax = 22
Shapes_Add()
t = 0
x = shX
y = shY
cx = x + 223 + 15
cy = y + 145 + 15
While t <= 80
  GraphicsWindow.Title = t
  MovePropeller()
  If (9 < t) And (t < 18) Then 
    y = y - 0.2
    cy = cy - 0.2
  EndIf
  If (43 < t) And (t < 67.5) Then 
    y = y + 0.4
    cy = cy + 0.4
  EndIf
  If (2 < t) And (t < 74.5) Then
    x = x - 2
    cx = cx - 2
    Shapes_Move()
  EndIf
  If (7 < t) And (t < 8) Then
    angle = angle + 0.1
    Shapes_Rotate()
  EndIf
  If t = 18 Then
    y = 20
    x = gw
    cx = x + 223 + 15
    cy = y + 145 + 15
    angle = 0
    Shapes_Rotate()
    Shapes_Move()
    gy = gh
    Shapes.Move(ground, 0, gy)
  EndIf
  If t = 40 Then
    y = 20
    x = gw
    Shapes_Move()
    cx = x + 223 + 15
    cy = y + 145 + 15
  EndIf
  If (47.2 <= t) And (t < 60) Then
    gy = gy - 0.5
    Shapes.Move(ground, 0, gy)
  EndIf
  If t = 60 Then
    x = gw
    Shapes_Move()
    cx = x + 223 + 15
  EndIf
  If (43 < t) And (t < 44) Then
    angle = angle - 0.1
    Shapes_Rotate()
  EndIf
  If (50 < t) And (t < 52) Then
    angle = angle + 0.1
    Shapes_Rotate()
  EndIf
  If (69 < t) And (t < 70) Then
    angle = angle - 0.1
    Shapes_Rotate()
  EndIf
  t = t + 0.05
  Program.Delay(50)
EndWhile

Sub MovePropeller
  rt = Math.Remainder(t, 0.3)
  If rt < 0.15 Then
    Shapes.SetOpacity(shape[16]["obj"], 50)
    Shapes.SetOpacity(shape[17]["obj"], 50)
  Else
    Shapes.SetOpacity(shape[16]["obj"], 100)
    Shapes.SetOpacity(shape[17]["obj"], 100)
  EndIf
EndSub

Sub Shapes_Init
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 400 ' x offset
  shY = 215 ' y offset
  shape = ""
  shape[1] = "func=rect;x=162;y=84;width=26;height=5;angle=26;bc=#FF0000;pw=0;"
  shape[2] = "func=rect;x=247;y=96;width=26;height=5;angle=339;bc=#FF0000;pw=0;"
  shape[3] = "func=tri;x=214;y=116;x1=6;y1=0;x2=0;y2=49;x3=12;y3=49;angle=137;bc=#FFFFFF;pw=0;"
  shape[4] = "func=tri;x=48;y=115;x1=6;y1=0;x2=0;y2=49;x3=12;y3=49;angle=220;bc=#FFFFFF;pw=0;"
  shape[5] = "func=ell;x=84;y=55;width=215;height=78;bc=#FFFFFF;pw=0;"
  shape[6] = "func=rect;x=54;y=86;width=164;height=47;bc=#FFFFFF;pw=0;"
  shape[7] = "func=ell;x=6;y=86;width=95;height=46;bc=#FFFFFF;pw=0;"
  shape[8] = "func=tri;x=286;y=13;x1=33;y1=0;x2=0;y2=122;x3=67;y3=122;angle=76;bc=#FFFFFF;pw=0;"
  shape[9] = "func=tri;x=326;y=1;x1=23;y1=0;x2=0;y2=59;x3=47;y3=59;bc=#FFFFFF;pw=0;"
  shape[10] = "func=rect;x=349;y=0;width=29;height=60;bc=#FFFFFF;pw=0;"
  shape[11] = "func=rect;x=142;y=59;width=90;height=28;bc=#FFFFFF;pw=0;"
  shape[12] = "func=ell;x=140;y=47;width=128;height=19;bc=#FFFFFF;pc=#999999;pw=1;"
  shape[13] = "func=ell;x=163;y=67;width=24;height=25;bc=#000000;pw=0;"
  shape[14] = "func=ell;x=250;y=67;width=24;height=25;bc=#000000;pw=0;"
  shape[15] = "func=ell;x=207;y=67;width=24;height=25;bc=#000000;pw=0;"
  shape[16] = "func=ell;x=0;y=61;width=9;height=44;bc=#939393;pw=0;"
  shape[17] = "func=ell;x=0;y=113;width=9;height=44;bc=#939393;pw=0;"
  shape[18] = "func=ell;x=22;y=145;width=30;height=30;bc=#939393;pc=#000000;pw=8;"
  shape[19] = "func=ell;x=223;y=145;width=30;height=30;bc=#939393;pc=#000000;pw=8;"
  shape[20] = "func=tri;x=79;y=65;x1=38;y1=0;x2=0;y2=22;x3=76;y3=22;bc=#000000;pw=0;"
  shape[21] = "func=rect;x=117;y=65;width=20;height=22;bc=#000000;pw=0;"
  shape[22] = "func=rect;x=134;y=65;width=23;height=28;bc=#FFFFFF;pw=0;"
EndSub

Sub Math_CartesianToPolar
  ' Math | convert cartesian coodinate to polar coordinate
  ' param x, y - cartesian coordinate
  ' return r, a - polar coordinate
  r = Math.SquareRoot(x * x + y * y)
  If x = 0 And y > 0 Then
    a = 90 ' [degree]
  ElseIf x = 0 And y < 0 Then
    a = -90
  Else
    a = Math.ArcTan(y / x) * 180 / Math.Pi
  EndIf
  If x < 0 Then
    a = a + 180
  ElseIf x > 0 And y < 0 Then
    a = a + 360
  EndIf
EndSub

Sub SB_RotateWorkaround
  ' Small Basic | Rotate workaround for Silverlight
  ' param shp - current shape
  ' param x, y - original coordinate
  ' param alpha - angle [radian]
  ' returns x, y - workaround coordinate
  If shp["func"] = "tri" Then
    x1 = -Math.Floor(shp["x3"] / 2)
    y1 = -Math.Floor(shp["y3"] / 2)
  ElseIf shp["func"] = "line" Then
    x1 = -Math.Floor(Math.Abs(shp["x1"] - shp["x2"]) / 2)
    y1 = -Math.Floor(Math.Abs(shp["y1"] - shp["y2"]) / 2)
  EndIf
  ox = x - x1
  oy = y - y1
  x = x1 * Math.Cos(alpha) - y1 * Math.Sin(alpha) + ox
  y = x1 * Math.Sin(alpha) + y1 * Math.Cos(alpha) + oy
EndSub

Sub SB_Workaround
  ' Small Basic | Workaround for Silverlight
  ' returns silverlight - "True" if in remote
  color = GraphicsWindow.GetPixel(0, 0)
  If Text.GetLength(color) > 7 Then
    silverlight = "True"
    msWait = 300
  Else
    silverlight = "False"
  EndIf
EndSub

Sub Shapes_Add
  ' Shapes | add shapes as shapes data
  ' param iMin, iMax - shape indices to add
  ' param shape - array of shapes
  ' param scale - 1 if same scale
  ' return shWidth, shHeight - total size of shapes
  ' return shAngle - current angle of shapes
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  Shapes_CalcWidthAndHeight()
  s = scale
  For i = iMin To iMax
    shp = shape[i]
    GraphicsWindow.PenWidth = shp["pw"] * s
    If shp["pw"] > 0 Then
      GraphicsWindow.PenColor = shp["pc"]
    EndIf
    If Text.IsSubText("rect|ell|tri|text", shp["func"]) Then
      GraphicsWindow.BrushColor = shp["bc"]
    EndIf
    If shp["func"] = "rect" Then
      shp["obj"] = Shapes.AddRectangle(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "ell" Then
      shp["obj"] = Shapes.AddEllipse(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "tri" Then
      shp["obj"] = Shapes.AddTriangle(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s, shp["x3"] * s, shp["y3"] * s)
    ElseIf shp["func"] = "line" Then
      shp["obj"] = Shapes.AddLine(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s)
    ElseIf shp["func"] = "text" Then
      If silverlight Then
        fs = Math.Floor(shp["fs"] * 0.9)
      Else
        fs = shp["fs"]
      EndIf
      GraphicsWindow.FontSize = fs * s
      GraphicsWindow.FontName = shp["fn"]
      shp["obj"] = Shapes.AddText(shp["text"])
    EndIf
    x = shp["x"]
    y = shp["y"]
    shp["rx"] = x
    shp["ry"] = y
    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      alpha = Math.GetRadians(shp["angle"])
      SB_RotateWorkaround()
      shp["wx"] = x
      shp["wy"] = y
    EndIf
    Shapes.Move(shp["obj"], shX + x * s, shY + y * s)
    If Text.IsSubText("rect|ell|tri|text", shp["func"]) And (shp["angle"] <> 0) And (shp["angle"] <> "") Then
      Shapes.Rotate(shp["obj"], shp["angle"])
    EndIf
    shape[i] = shp
  EndFor
  shAngle = 0
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub

Sub Shapes_CalcRotatePos
  ' Shapes | Calculate position for rotated shape
  ' param["x"], param["y"] - position of a shape
  ' param["width"], param["height"] - size of a shape
  ' param ["cx"], param["cy"] - center of rotation
  ' param ["angle"] - rotate angle
  ' return x, y - rotated position of a shape
  _cx = param["x"] + param["width"] / 2
  _cy = param["y"] + param["height"] / 2
  x = _cx - param["cx"]
  y = _cy - param["cy"]
  Math_CartesianToPolar()
  a = a + param["angle"]
  x = r * Math.Cos(a * Math.Pi / 180)
  y = r * Math.Sin(a * Math.Pi / 180)
  _cx = x + param["cx"]
  _cy = y + param["cy"]
  x = _cx - param["width"] / 2
  y = _cy - param["height"] / 2
EndSub

Sub Shapes_CalcWidthAndHeight
  ' Shapes | Calculate total width and height of shapes
  ' param iMin, iMax - shape indices to add
  ' return shWidth, shHeight - total size of shapes
  For i = iMin To iMax
    shp = shape[i]
    If shp["func"] = "tri" Or shp["func"] = "line" Then
      xmin = shp["x1"]
      xmax = shp["x1"]
      ymin = shp["y1"]
      ymax = shp["y1"]
      If shp["x2"] < xmin Then
        xmin = shp["x2"]
      EndIf
      If xmax < shp["x2"] Then
        xmax = shp["x2"]
      EndIf
      If shp["y2"] < ymin Then
        ymin = shp["y2"]
      EndIf
      If ymax < shp["y2"] Then
        ymax = shp["y2"]
      EndIf
      If shp["func"] = "tri" Then
        If shp["x3"] < xmin Then
          xmin = shp["x3"]
        EndIf
        If xmax < shp["x3"] Then
          xmax = shp["x3"]
        EndIf
        If shp["y3"] < ymin Then
          ymin = shp["y3"]
        EndIf
        If ymax < shp["y3"] Then
          ymax = shp["y3"]
        EndIf
      EndIf
      shp["width"] = xmax - xmin
      shp["height"] = ymax - ymin
    EndIf
    If i = 1 Then
      shWidth = shp["x"] + shp["width"]
      shHeight = shp["y"] + shp["height"]
    Else
      If shWidth < shp["x"] + shp["width"] Then
        shWidth = shp["x"] + shp["width"]
      EndIf
      If shHeight < shp["y"] + shp["height"] Then
        shHeight = shp["y"] + shp["height"]
      EndIf
    EndIf
    shape[i] = shp
  EndFor
EndSub

Sub Shapes_Move
  ' Shapes | Move shapes
  ' param iMin, iMax - shape indices to add
  ' param shape - array of shapes
  ' param scale - to zoom
  ' param x, y - position to move
  ' return shX, shY - new position of shapes
  Stack.PushValue("local", i)
  s = scale
  shX = x
  shY = y
  For i = iMin To iMax
    shp = shape[i]
    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      _x = shp["wx"]
      _y = shp["wy"]
    Else
      _x = shp["rx"]
      _y = shp["ry"]
    EndIf
    Shapes.Move(shp["obj"], shX + _x * s, shY + _y * s)
  EndFor
  i = Stack.PopValue("local")
EndSub

Sub Shapes_Remove
  ' Shapes | Remove shapes
  ' param iMin, iMax - shapes indices to remove
  ' param shape - array of shapes
  Stack.PushValue("local", i)
  For i = iMin To iMax
    shp = shape[i]
    Shapes.Remove(shp["obj"])
  EndFor
  i = Stack.PopValue("local")
EndSub

Sub Shapes_Rotate
  ' Shapes | Rotate shapes
  ' param iMin, iMax - shapes indices to rotate
  ' param shape - array of shapes
  ' param cx, cy - rotation center
  ' param scale - to zoom
  ' param angle - to rotate
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  s = scale
  param["angle"] = angle
  If cx <> "" Then
    param["cx"] = cx
  Else
    cx = ""   ' to avoid syntax error
    param["cx"] = shWidth / 2
  EndIf
  If cy <> "" Then
    param["cy"] = cy
  Else
    cy = ""   ' to avoid syntax error
    param["cy"] = shHeight / 2
  EndIf
  For i = iMin To iMax
    shp = shape[i]
    param["x"] = shp["x"]
    param["y"] = shp["y"]
    param["width"] = shp["width"]
    param["height"] = shp["height"]
    Shapes_CalcRotatePos()
    shp["rx"] = x
    shp["ry"] = y
    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      alpha = Math.GetRadians(angle + shp["angle"])
      SB_RotateWorkAround()
      shp["wx"] = x
      shp["wy"] = y
    EndIf
    Shapes.Move(shp["obj"], shX + x * s, shY + y * s)
    Shapes.Rotate(shp["obj"], angle + shp["angle"])
    shape[i] = shp
  EndFor
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub
