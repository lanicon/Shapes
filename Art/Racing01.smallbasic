' 04/04/2013 21:10:19 Shapes generated by Shapes 1.5
'
GraphicsWindow.Title = "Racing 0.1"
GraphicsWindow.BackgroundColor = "GreenYellow"
GraphicsWindow.BrushColor = "DimGray"
x0 = 7
y0 = 6
GraphicsWindow.FillEllipse(x0, y0, 430, 430)
GraphicsWindow.FillEllipse(x0 + 180, y0, 430, 430)
GraphicsWindow.FillRectangle(x0 + 215, y0, 175, 430)
GraphicsWindow.BrushColor = "GreenYellow"
GraphicsWindow.FillEllipse(x0 + 140, y0 + 140, 140, 140)
GraphicsWindow.FillEllipse(x0 + 320, y0 + 140, 140, 140)
GraphicsWindow.FillRectangle(x0 + 215, y0 + 140, 175, 140)
GraphicsWindow.BrushColor = "White"
GraphicsWindow.FillRectangle(x0 + 300, y0 + 280, 4, 150)
' initialize shapes
SB_Workaround()
Shapes_Init()
' add shapes
scale = 0.2
iMin = 1
iMax = 32
Shapes_Add()
angle = 50
Shapes_Rotate()
color1 = "#C70000"
color2 = "#007700"
num1 = 1
num2 = 2
ChangeCar()
Shapes_Add()
x = 300
y = 300
Shapes_Move()
color1 = "#007700"
color2 = "#0000C7"
num1 = 2
num2 = 3
ChangeCar()
Shapes_Add()
x = 400
y = 360
Shapes_Move()
angle = -5
Shapes_Rotate()
Sub ChangeCar
  ' param color1 - from
  ' param color2 - to
  ' param num1 - from
  ' param num2 - to
  For i = iMin To iMax
    If shape[i]["bc"] = color1 Then
      shape[i]["bc"] = color2
    ElseIf shape[i]["text"] = num1 Then
      shape[i]["text"] = num2
    EndIf
  EndFor
EndSub
Sub Shapes_Init
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 135 ' x offset
  shY = 300 ' y offset
  shape = ""
  shape[1] = "func=line;x=58;y=44;x1=0;y1=0;x2=0;y2=104;pc=#939393;pw=4;"
  shape[2] = "func=line;x=294;y=39;x1=1;y1=0;x2=0;y2=117;pc=#939393;pw=4;"
  shape[3] = "func=rect;x=9;y=63;width=279;height=63;bc=#C70000;pw=0;"
  shape[4] = "func=ell;x=276;y=63;width=24;height=63;bc=#C70000;pw=0;"
  shape[5] = "func=ell;x=0;y=64;width=18;height=61;bc=#C70000;pw=0;"
  shape[6] = "func=ell;x=4;y=74;width=14;height=41;bc=#000000;pw=0;"
  shape[7] = "func=rect;x=258;y=0;width=75;height=40;bc=#242424;pw=0;"
  shape[8] = "func=rect;x=22;y=7;width=75;height=40;bc=#242424;pw=0;"
  shape[9] = "func=rect;x=21;y=145;width=75;height=40;bc=#242424;pw=0;"
  shape[10] = "func=rect;x=257;y=149;width=75;height=40;bc=#242424;pw=0;"
  shape[11] = "func=ell;x=169;y=74;width=37;height=41;bc=#80AAC1;pw=0;"
  shape[12] = "func=ell;x=175;y=74;width=39;height=41;bc=#000000;pw=0;"
  shape[13] = "func=rect;x=194;y=75;width=59;height=40;bc=#000000;pw=0;"
  shape[14] = "func=ell;x=238;y=74;width=27;height=42;bc=#000000;pw=0;"
  shape[15] = "func=ell;x=54;y=75;width=39;height=39;bc=#FFFFFF;pw=0;"
  shape[16] = "func=rect;x=190;y=78;width=40;height=9;bc=#C70000;pw=0;"
  shape[17] = "func=rect;x=188;y=102;width=42;height=8;bc=#C70000;pw=0;"
  shape[18] = "func=ell;x=214;y=84;width=24;height=23;bc=#000000;pw=0;"
  shape[19] = "func=ell;x=220;y=83;width=24;height=25;bc=#C70000;pw=0;"
  shape[20] = "func=line;x=22;y=15;x1=0;y1=0;x2=73;y2=0;pc=#939393;pw=1;"
  shape[21] = "func=line;x=22;y=25;x1=0;y1=0;x2=73;y2=0;pc=#939393;pw=1;"
  shape[22] = "func=line;x=22;y=36;x1=0;y1=0;x2=72;y2=0;pc=#939393;pw=1;"
  shape[23] = "func=line;x=21;y=153;x1=0;y1=0;x2=73;y2=0;pc=#939393;pw=1;"
  shape[24] = "func=line;x=20;y=164;x1=0;y1=0;x2=74;y2=0;pc=#939393;pw=1;"
  shape[25] = "func=line;x=21;y=174;x1=0;y1=1;x2=73;y2=0;pc=#939393;pw=1;"
  shape[26] = "func=line;x=258;y=8;x1=0;y1=0;x2=73;y2=0;pc=#939393;pw=1;"
  shape[27] = "func=line;x=258;y=19;x1=0;y1=0;x2=73;y2=0;pc=#939393;pw=1;"
  shape[28] = "func=line;x=258;y=30;x1=0;y1=0;x2=73;y2=0;pc=#939393;pw=1;"
  shape[29] = "func=line;x=257;y=156;x1=0;y1=0;x2=73;y2=0;pc=#939393;pw=1;"
  shape[30] = "func=line;x=257;y=167;x1=0;y1=1;x2=73;y2=0;pc=#939393;pw=1;"
  shape[31] = "func=line;x=257;y=178;x1=0;y1=0;x2=74;y2=0;pc=#939393;pw=1;"
  shape[32] = "func=text;x=65;y=80;text=1;fs=24;angle=90;fn=Arial;bc=#000000;"
EndSub
Sub Math_CartesianToPolar
  ' Math | convert cartesian coodinate to polar coordinate
  ' param x, y - cartesian coordinate
  ' return r, a - polar coordinate
  r = Math.SquareRoot(x * x + y * y)
  If x = 0 And y > 0 Then
    a = 90 ' [degree]
  ElseIf x = 0 And y < 0 Then
    a = -90
  Else
    a = Math.ArcTan(y / x) * 180 / Math.Pi
  EndIf
  If x < 0 Then
    a = a + 180
  ElseIf x > 0 And y < 0 Then
    a = a + 360
  EndIf
EndSub
Sub SB_RotateWorkaround
  ' Small Basic | Rotate workaround for Silverlight
  ' param x, y - original coordinate
  ' param alpha - angle [radian]
  ' returns x, y - workaround coordinate
  If shape[i]["func"] = "tri" Then
    x1 = -Math.Floor(shape[i]["x3"] / 2)
    y1 = -Math.Floor(shape[i]["y3"] / 2)
  ElseIf shape[i]["func"] = "line" Then
    x1 = -Math.Floor(Math.Abs(shape[i]["x1"] - shape[i]["x2"]) / 2)
    y1 = -Math.Floor(Math.Abs(shape[i]["y1"] - shape[i]["y2"]) / 2)
  EndIf
  ox = x - x1
  oy = y - y1
  x = x1 * Math.Cos(alpha) - y1 * Math.Sin(alpha) + ox
  y = x1 * Math.Sin(alpha) + y1 * Math.Cos(alpha) + oy
EndSub
Sub SB_Workaround
  ' Small Basic | Workaround for Silverlight
  ' returns silverlight - "True" if in remote
  color = GraphicsWindow.GetPixel(0, 0)
  If Text.GetLength(color) > 7 Then
    silverlight = "True"
    msWait = 300
  Else
    silverlight = "False"
  EndIf
EndSub
Sub Shapes_Add
  ' Shapes | add shapes as shapes data
  ' param iMin, iMax - shape indices to add
  ' param shape - array of shapes
  ' param scale - 1 if same scale
  ' return shWidth, shHeight - total size of shapes
  ' return shAngle - current angle of shapes
  Stack.PushValue("local", i)
  Shapes_CalcWidthAndHeight()
  s = scale
  For i = iMin To iMax
    GraphicsWindow.PenWidth = shape[i]["pw"] * s
    If shape[i]["pw"] > 0 Then
      GraphicsWindow.PenColor = shape[i]["pc"]
    EndIf
    If Text.IsSubText("rect|ell|tri|text", shape[i]["func"]) Then
      GraphicsWindow.BrushColor = shape[i]["bc"]
    EndIf
    If shape[i]["func"] = "rect" Then
      shape[i]["obj"] = Shapes.AddRectangle(shape[i]["width"]* s, shape[i]["height"] * s)
    ElseIf shape[i]["func"] = "ell" Then
      shape[i]["obj"] = Shapes.AddEllipse(shape[i]["width"]* s, shape[i]["height"] * s)
    ElseIf shape[i]["func"] = "tri" Then
      shape[i]["obj"] = Shapes.AddTriangle(shape[i]["x1"] * s, shape[i]["y1"] * s, shape[i]["x2"] * s, shape[i]["y2"] * s, shape[i]["x3"] * s, shape[i]["y3"] * s)
    ElseIf shape[i]["func"] = "line" Then
      shape[i]["obj"] = Shapes.AddLine(shape[i]["x1"] * s, shape[i]["y1"] * s, shape[i]["x2"] * s, shape[i]["y2"] * s)
    ElseIf shape[i]["func"] = "text" Then
      If silverlight Then
        fs = Math.Floor(shape[i]["fs"] * 0.9)
      Else
        fs = shape[i]["fs"]
      EndIf
      GraphicsWindow.FontSize = fs * s
      GraphicsWindow.FontName = shape[i]["fn"]
      shape[i]["obj"] = Shapes.AddText(shape[i]["text"])
    EndIf
    If silverlight And shape[i]["func"] = "tri" Then
      alpha = Math.GetRadians(shape[i]["angle"])
      x1 = -Math.Floor(shape[i]["x3"] / 2)
      y1 = -Math.Floor(shape[i]["y3"] / 2)
      ox = shape[i]["x"] - x1
      oy = shape[i]["y"] - y1
      r = Math.SquareRoot(x1 * x1 + y1 * y1)
      x = x1 * Math.Cos(alpha) - y1 * Math.Sin(alpha) + ox
      y = x1 * Math.Sin(alpha) + y1 * Math.Cos(alpha) + oy
      Shapes.Move(shape[i]["obj"], shX + x * s, shY + y * s)
    Else
      Shapes.Move(shape[i]["obj"], shX + shape[i]["x"] * s, shY + shape[i]["y"] * s)
    EndIf
    If Text.IsSubText("rect|ell|tri|text", shape[i]["func"]) And shape[i]["angle"] <> 0 Then
      Shapes.Rotate(shape[i]["obj"], shape[i]["angle"])
    EndIf
    shape[i]["rx"] = shape[i]["x"]
    shape[i]["ry"] = shape[i]["y"]
  EndFor
  shAngle = 0
  i = Stack.PopValue("local")
EndSub
Sub Shapes_CalcRotatePos
  ' Shapes | Calculate position for rotated shape
  ' param["x"], param["y"] - position of a shape
  ' param["width"], param["height"] - size of a shape
  ' param ["cx"], param["cy"] - center of rotation
  ' param ["angle"] - rotate angle
  ' return x, y - rotated position of a shape
  _cx = param["x"] + param["width"] / 2
  _cy = param["y"] + param["height"] / 2
  x = _cx - param["cx"]
  y = _cy - param["cy"]
  Math_CartesianToPolar()
  a = a + param["angle"]
  x = r * Math.Cos(a * Math.Pi / 180)
  y = r * Math.Sin(a * Math.Pi / 180)
  _cx = x + param["cx"]
  _cy = y + param["cy"]
  x = _cx - param["width"] / 2
  y = _cy - param["height"] / 2
EndSub
Sub Shapes_CalcWidthAndHeight
  ' Shapes | Calculate total width and height of shapes
  ' param iMin, iMax - shape indices to add
  ' return shWidth, shHeight - total size of shapes
  For i = iMin To iMax
    If shape[i]["func"] = "tri" Or shape[i]["func"] = "line" Then
      xmin = shape[i]["x1"]
      xmax = shape[i]["x1"]
      ymin = shape[i]["y1"]
      ymax = shape[i]["y1"]
      If shape[i]["x2"] < xmin Then
        xmin = shape[i]["x2"]
      EndIf
      If xmax < shape[i]["x2"] Then
        xmax = shape[i]["x2"]
      EndIf
      If shape[i]["y2"] < ymin Then
        ymin = shape[i]["y2"]
      EndIf
      If ymax < shape[i]["y2"] Then
        ymax = shape[i]["y2"]
      EndIf
      If shape[i]["func"] = "tri" Then
        If shape[i]["x3"] < xmin Then
          xmin = shape[i]["x3"]
        EndIf
        If xmax < shape[i]["x3"] Then
          xmax = shape[i]["x3"]
        EndIf
        If shape[i]["y3"] < ymin Then
          ymin = shape[i]["y3"]
        EndIf
        If ymax < shape[i]["y3"] Then
          ymax = shape[i]["y3"]
        EndIf
      EndIf
      shape[i]["width"] = xmax - xmin
      shape[i]["height"] = ymax - ymin
    EndIf
    If i = 1 Then
      shWidth = shape[i]["x"] + shape[i]["width"]
      shHeight = shape[i]["y"] + shape[i]["height"]
    Else
      If shWidth < shape[i]["x"] + shape[i]["width"] Then
        shWidth = shape[i]["x"] + shape[i]["width"]
      EndIf
      If shHeight < shape[i]["y"] + shape[i]["height"] Then
        shHeight = shape[i]["y"] + shape[i]["height"]
      EndIf
    EndIf
  EndFor
EndSub
Sub Shapes_Move
  ' Shapes | Move shapes
  ' param iMin, iMax - shape indices to add
  ' param shape - array of shapes
  ' param scale - to zoom
  ' param x, y - position to move
  ' return shX, shY - new position of shapes
  Stack.PushValue("local", i)
  s = scale
  shX = x
  shY = y
  For i = iMin To iMax
    _x = shape[i]["rx"]
    _y = shape[i]["ry"]
    Shapes.Move(shape[i]["obj"], shX + _x * s, shY + _y * s)
  EndFor
  i = Stack.PopValue("local")
EndSub
Sub Shapes_Remove
  ' Shapes | Remove shapes
  ' param iMin, iMax - shapes indices to remove
  ' param shape - array of shapes
  Stack.PushValue("local", i)
  For i = iMin To iMax
    Shapes.Remove(shape[i]["obj"])
  EndFor
  i = Stack.PopValue("local")
EndSub
Sub Shapes_Rotate
  ' Shapes | Rotate shapes
  ' param iMin, iMax - shapes indices to rotate
  ' param shape - array of shapes
  ' param scale - to zoom
  ' param angle - to rotate
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  s = scale
  param["angle"] = angle
  param["cx"] = shWidth / 2
  param["cy"] = shHeight / 2
  For i = iMin To iMax
    param["x"] = shape[i]["x"]
    param["y"] = shape[i]["y"]
    param["width"] = shape[i]["width"]
    param["height"] = shape[i]["height"]
    Shapes_CalcRotatePos()
    If silverlight And Text.IsSubText("tri|line", shape[i]["func"]) Then
      alpha = Math.GetRadians(angle + shape[i]["angle"])
      SB_RotateWorkAround()
    EndIf
    shape[i]["rx"] = x
    shape[i]["ry"] = y
    Shapes.Move(shape[i]["obj"], shX + x * s, shY + y * s)
    Shapes.Rotate(shape[i]["obj"], angle + shape[i]["angle"])
  EndFor
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub
